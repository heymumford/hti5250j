plugins {
    id 'java'
}

group = 'org.hti5250j'
version = findProperty('version') ?: '0.8.0-headless.0.rtr'

def runtimeLibs = fileTree(dir: 'lib/runtime', include: ['*.jar', '*.zip'])

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation runtimeLibs

    testImplementation runtimeLibs
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.awaitility:awaitility:4.3.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
}

sourceSets {
    main {
        java { srcDirs = ['src'] }
        resources { srcDirs = ['resources'] }
    }
    test {
        java { srcDirs = ['tests'] }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()

    def maxForks = (findProperty('test.maxParallelForks') ?: '4') as int
    def forkEvery = (findProperty('test.forkEvery') ?: '0') as int
    def parallelEnabled = (findProperty('test.junit.parallel.enabled') ?: 'true').toString()
    def parallelMode = (findProperty('test.junit.parallel.mode') ?: 'concurrent').toString()
    def parallelModeClasses = (findProperty('test.junit.parallel.mode.classes') ?: 'concurrent').toString()

    maxParallelForks = Math.max(1, maxForks)
    if (forkEvery > 0) {
        forkEvery = forkEvery
    }

    systemProperty 'junit.jupiter.execution.parallel.enabled', parallelEnabled
    systemProperty 'junit.jupiter.execution.parallel.mode.default', parallelMode
    systemProperty 'junit.jupiter.execution.parallel.mode.classes.default', parallelModeClasses

    testLogging {
        events 'failed', 'skipped'
    }
}
